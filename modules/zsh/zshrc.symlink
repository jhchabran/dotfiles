autoload -Uz compinit
compinit

# shortcut to this dotfiles path is $DOTFILES_PATH
export DOTFILES_PATH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/code
export MYCODE=~/code/src/github.com/jhchabran

if [ -f "/Applications/Emacs.app/Contents/MacOS/Emacs" ]; then
  export EMACS="/Applications/Emacs.app/Contents/MacOS/Emacs"
  alias emacs="$EMACS -nw"
fi

if [ -f "/Applications/Emacs.app/Contents/MacOS/bin/emacsclient" ]; then
  alias emacsclient="/Applications/Emacs.app/Contents/MacOS/bin/emacsclient"
fi

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  #echo "Loading ~/.localrc ..."
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES_PATH/modules/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  #echo "Loading path $file ..."
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  #echo "Loading all but path and completion $file ..."
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  #echo "Loading completion $file ..."
  source $file
done

unset config_files

# Automatically set by fzf
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# The next line updates PATH for the Google Cloud SDK.
if [ -f '/Users/tech/Downloads/google-cloud-sdk/path.zsh.inc' ]; then . '/Users/tech/Downloads/google-cloud-sdk/path.zsh.inc'; fi

# The next line enables shell command completion for gcloud.
if [ -f '/Users/tech/Downloads/google-cloud-sdk/completion.zsh.inc' ]; then . '/Users/tech/Downloads/google-cloud-sdk/completion.zsh.inc'; fi
# The following lines were added by compinstall

zstyle ':completion:*' completer _complete _ignored _approximate
zstyle :compinstall filename '/Users/tech/.zshrc'

# End of lines added by compinstall
